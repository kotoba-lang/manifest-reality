(ns manifest-reality.checks-test
  (:require [clojure.test :refer [deftest is testing]]
            [manifest-reality.parse :as p]
            [manifest-reality.checks :as c]))

;; 実測に基づく fixture。stray-block は commit 6ebf4b4b5fc が実際に作った形
;; (loop-jiten の project ブロックが remotes: セクション内に挿入された)。
(def west-ok
  (str "manifest:\n"
       "  version: \"1.0\"\n\n"
       "  remotes:\n"
       "    - name: kotoba-lang\n"
       "      url-base: git@github.com:kotoba-lang\n\n"
       "  defaults:\n    revision: main\n\n"
       "  projects:\n"
       "    - name: alpha\n"
       "      remote: kotoba-lang\n"
       "      revision: aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa\n"
       "      path: orgs/kotoba-lang/alpha\n"
       "      groups: [kotoba-lang]\n"
       "\n  self:\n    path: manifest\n"))

(def west-stray
  (str "manifest:\n"
       "  version: \"1.0\"\n\n"
       "  remotes:\n"
       "    - name: kotoba-lang\n"
       "      url-base: git@github.com:kotoba-lang\n"
       "    - name: loop-jiten\n"
       "      remote: kotoba-lang\n"
       "      revision: bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb\n"
       "      path: orgs/kotoba-lang/loop-jiten\n"
       "      groups: [kotoba-lang]\n\n"
       "  defaults:\n    revision: main\n\n"
       "  projects:\n"
       "    - name: alpha\n"
       "      remote: kotoba-lang\n"
       "      revision: aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa\n"
       "      path: orgs/kotoba-lang/alpha\n"
       "      groups: [kotoba-lang]\n"
       "\n  self:\n    path: manifest\n"))

(deftest parse-separates-remotes-from-projects
  (let [w (p/parse-west west-ok)]
    (is (= 1 (count (p/projects w))) "remotes の {name,url-base} は project ではない")
    (is (= "orgs/kotoba-lang/alpha" (:path (first (p/projects w)))))))

(deftest stray-block-is-detected
  (testing "健全な west では 0 件"
    (is (empty? (c/stray-blocks (p/parse-west west-ok)))))
  (testing "remotes に混入した project ブロックを拾う"
    (let [f (c/stray-blocks (p/parse-west west-stray))]
      (is (= 1 (count f)))
      (is (= :west/stray-block (:check (first f))))
      (is (= :remotes (get-in (first f) [:detail :section]))))))

(deftest org-consistency-flags-mismatch
  (let [w (p/parse-west
           (str "manifest:\n  projects:\n"
                "    - name: beta\n      remote: gftdcojp\n"
                "      revision: cccccccccccccccccccccccccccccccccccccccc\n"
                "      path: orgs/kotoba-lang/beta\n      groups: [com-junkawasaki]\n"
                "\n  self:\n    path: manifest\n"))
        f (c/org-consistency w)]
    (is (= #{:west/remote-org-mismatch :west/groups-org-mismatch} (set (map :check f))))))

(deftest archived-and-datalad-groups-are-allowed
  (let [w (p/parse-west
           (str "manifest:\n  projects:\n"
                "    - name: g\n      remote: kotoba-lang\n"
                "      revision: dddddddddddddddddddddddddddddddddddddddd\n"
                "      path: orgs/kotoba-lang/g\n      groups: [archived]\n"
                "\n  self:\n    path: manifest\n"))]
    (is (empty? (c/org-consistency w)))))

(deftest stale-path-keys-detected
  (let [f (c/stale-path-keys {:rad-rids {"orgs/etzhayyim/com-etzhayyim-x" "rad:z1"
                                         "orgs/kotoba-lang/alpha" "rad:z2"}}
                             #{"orgs/kotoba-lang/alpha"})]
    (is (= 1 (count f)))
    (is (= "orgs/etzhayyim/com-etzhayyim-x" (:subject (first f))))))

(deftest orphan-and-unregistered
  (let [w (p/parse-west west-ok)]
    (is (= 1 (count (c/orphan-entries w #{}))) "GitHub に無ければ orphan")
    (is (empty? (c/orphan-entries w #{["kotoba-lang" "alpha"]})))
    (is (= 1 (count (c/unregistered w #{["kotoba-lang" "alpha"] ["kotoba-lang" "zeta"]} {}))))
    (is (empty? (c/unregistered w #{["kotoba-lang" "alpha"] ["kotoba-lang" "zeta"]}
                                {:ignore? #(= "zeta" (second %))})))))

(deftest duplicate-entries-detected
  (let [w (p/parse-west
           (str "manifest:\n  projects:\n"
                "    - name: a\n      remote: kotoba-lang\n"
                "      revision: 1111111111111111111111111111111111111111\n"
                "      path: orgs/kotoba-lang/a\n      groups: [kotoba-lang]\n"
                "    - name: b\n      remote: cloud-itonami\n"
                "      revision: 2222222222222222222222222222222222222222\n"
                "      path: orgs/cloud-itonami/b\n      groups: [cloud-itonami]\n"
                "\n  self:\n    path: manifest\n"))
        f (c/duplicate-entries w (constantly 42))]
    (is (= 1 (count f)))
    (is (= 2 (count (get-in (first f) [:detail :paths]))))))

(deftest credential-shape
  (is (c/credential-shaped? ["certs/distribution/AAA.p12" "README.md"]))
  (is (c/credential-shaped? ["profiles/appstore/X.mobileprovision"]))
  (is (not (c/credential-shaped? ["src/a.cljc" "test/a_test.cljc" "key.pem.example"]))
      ".pem 単体は公開鍵/fixture でも出るので採らない"))

(deftest audit-composes-and-splits-severity
  (let [f (c/audit {:west west-stray
                    :gh-repos #{["kotoba-lang" "alpha"] ["kotoba-lang" "loop-jiten"]}})]
    (is (seq (c/errors f)))
    (is (= 1 (count (filter #(= :west/stray-block (:check %)) f))))
    (is (map? (c/summary f)))))

(deftest audit-is-clean-on-healthy-input
  (is (empty? (c/audit {:west west-ok
                        :gh-repos #{["kotoba-lang" "alpha"]}
                        :keyed-maps {:rad-rids {"orgs/kotoba-lang/alpha" "rad:z"}}}))))
